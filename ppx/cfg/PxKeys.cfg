;・キーバインド
; ※;@はデフォルトコマンド、;;@は上書きしたデフォルトコマンド
;/ KC_main/ KC_incs/ K_edit/ K_ppe/ K_lied/ K_tree/
;/ KB_edit/ KV_main/  KV_page/  KV_crt/  KV_img/
;==========================================================================
;**PPc
;==========================================================================
-|KC_main =
KC_main = {
FIRSTEVENT  , *string i,cID=%*regexp("%n","/C(.)/$1/") %:*string i,Pver=%*script(%*RESULT version)
;SELECTEVENT , *comment 1,extract,%*script(%*RESULT filetype)
\V_H30      , *addhistory u,%FD             ;shift + "0"
              *linemessage Bookmark > %FD
\^V_H30     , %M_pjump1,W               ;shift + ctrl + "0"
1           , %j"%*getcust(_User:pk1)"
2           , %j"%*getcust(_User:pk2)"
3           , %j"%*getcust(_User:pk3)"
4           , %j"%*getcust(_User:pk4)"
5           , %j"%*getcust(_User:pk5)"
6           , %j"%*getcust(_User:pk6)"
;7          , %j"%*getcust(_User:pk7)"
;8          , %j"%*getcust(_User:pk8)"
9           , %j"D:\deleted$"
'!'         , %K~,"1"
'"'         , %K~,"2"
'#'         , %K~,"3"
'$'         , %K~,"4"
'%'         , %K~,"5"
'&'         , %K~,"6"
;'''        , %K~,"7"
;'('        , %K~,"8"
')'         , %K~,"9"
;@'-'        マーク解除(ダイアログ)
;@&'-'       タブメニュー
;@'+'        マーク(ダイアログ)
;@';'        表示形式変更(MC_celS)
;@'>'        (=\PgDn,^PgDn)  末尾に移動
;@'<'        (=\PgUp,^PgUp)  先頭に移動
;@'^'        メニューバーON/OFF
'['         , *comment edit
'{'         , *comment clear
              *linemessage !"コメント削除
;;@'*'       すべて選択(※除ディレクトリ)
'@'         , *setcust K_lied:LEFT,*replace %%*regexp(%%*edittext,"s/(.*\\)(?!$).*/$1/") %%:*completelist %%:%%K"@F2
              *setcust K_lied:RIGHT,*replace %%*edittext%%\ %%:%%K"@F4@TAB
              *setcust K_lied:F12,%%0fenrirScan.exe %%&*completelist /reload
              *setcust K_lied:'@',*RotateExecute ep,%%%%k"\BS ..'\', %%%%k"\BS
              *setcust K_lied:\ENTER,*execute ~,*jumppath %%*edittext%%\
              *setcust K_lied:^s,%On *ppb -c echo +%%*edittext >> %%0ScanRule.ini
              *jumppath "%*input("..\" -title:"パス移動" -mode:e -k:"*editmode d %%:*setcust _User:ep=1")"
;;'@'         , *cd %0..\fenrir\PPx
;              %Oi %0..\fenrir\fenrir.exe /t
;              *fitwindow %NC#,%*findwindowclass("fenrirMainWindow"),20
':'         , *setcust XC_page=0
              *setcust XC_celD=_DBLA,_AUTO,6,7
              *setcust XC_mvUD=1,1,5,B0001,5,B0011
              *ppc -r -max -single -mps -bootid:X "%1" -k *execute CX,*maskentry .bmp,.jpg,.jpeg,.png,.gif,.vch,.edg,.mpg,p:*
'\'         , %K"@BS"
'.'         , *comment 1,extract,%*script(%*RESULT filetype)
APPS        , %ME_rcr
^APPS       , *string i,copyCloud=1
              %M_Dirmenu,W
;;@         , ディレクトリメニュー
^&APPS      , %M_menuCTool,S
;@DEL       エントリ位置記憶
\Down       , *cursor 16,1,3,B0010,5,B0011  ;次のマークに移動
;;@\Down(=PgDn)  ページ移動
^Down       , %K~"@Down"
\^Down      , *JSCRIPT changeDirNext.js
END         , *pane select t+1
;@Enter     実行
\ENTER      , %ME_scr   ;デフォルトの動作ではディレクトリの判定が異なる
;;@\Enter    ファイル判別
&ENTER      , %K"@="%K~"C_DIR
;@^ENTER(=\F10)  ファイル判別メニュー
~ENTER      , %j"%FDC::listfile"
;@&\ENTER   連動プロパティ
;@ESC       終了(確認あり)
HOME        , *pane select t-1
;@INS       エントリ位置移動
;@^LEFT     前に戻る
;@^\LEFT    まとめて前に戻る
;@Pause     処理中止
;@^Pause    応答無し状態の解消(※Vista以降)
;@^RIGHT    次に進む
;@^\RIGHT    まとめて次に進む
^SPACE      , *JSCRIPT toggleHighlight.js
TAB         , *JSCRIPT keyTAB_PPc.js
;;@\TAB       ,*Pane NewTab
\UP         , *cursor 16,-1,3,B0010,5,B0011 ;前のマークに移動
;;@\UP(=PgUp)  ページ移動 
^Up         , %K~"@Up
\^Up        , *JSCRIPT changeDirPrev.js
F1          , *help %"PPxHELP Topic"%E
;@F2      リネーム
;@\F2(=I) ドライブ情報
F3        , *if %*script(%*RESULT syncview) != 0 %:%KVZ,"]" %:*stop
\F3       , *if %*script(%*RESULT syncview) != 0 %:%KVZ,"[" %:*stop
;;@F3(=^F) エクスプローラの検索
F4        , *ppe %R
;;@F4(=T)  ツリー表示
\F4       , *ppe -new -k *setcaption PPe
;@&F4       全終了
;;@^F5      表示ディレクトリの更新
F6        , editorC %0script\xTest.js
\F6       , *JSCRIPT xTest.js
            *linemessage RUN > xTest.js
^\F6      , *linemessage Result > %*script(%0script\xTest.js)
F8        , %M?layoutmenu,!P    ;スクロールバー縦横切り替え
F9        = CUSTOMIZE
^F9       , %M_menuCView,!A
F10       , *RotateExecute C_pos,"*windowposition %%N.,680,300","*windowposition %%N.,230,120","*windowposition %%N.,350,200"
;;@F10     メニューバーにフォーカス移動
;@\F10(=^ENTER)  ファイル判別メニュー
;@^\F10     Shell Context Menu
;@F11       新規タブ
\F11      , *JSCRIPT callNewPPc.js
;;@\F11     同パス新規タブ
F12       , *JSCRIPT rotateThumb.js
;;@F12      ファイル複製
;@\F12      ハードリンク作成
;@A         属性変更
B         , *JSCRIPT markEXT.js
;;@B       , %"Binary Convert" %M_bin
;;\B       , *countsize
C         , *string i,copyCloud=0
            *MENUTYPE %:%M_FileCOPY,C
;;@C        コピー
\C        , %M_menuCEdit1
;@^C        エクスプローラ互換コピー
;@^\C       ディレクトリ名をクリップ
D         , *ifmatch "/aux://*/",%1 %:%K"@\D" %:*stop
            *if %*script(%*RESULT dirtype) >= 62 %:%"ファイル操作" %Q"書庫から削除します" %:%u7-zip64.dll,d -hide %FD %@ %:*stop
            *if %*script(%*RESULT dirtype) == 4 %:%K"@^\D" %:*stop
            %"ファイル操作" %Q"選択エントリを削除します" %:%Oa *ppcfile !safedelete,%*name(HP,%1),/qstart /min /retry:0 /error:0
;;@D        ゴミ箱へ
\D        , %0..\FastCopy\FastCopy.exe /open_window /cmd=delete /wipe_del /auto_close %#FDC
;;@\D       削除
;@^\D       リストファイルから削除
;@^D        自動D&D
E         , *if %*script(%*RESULT markcount) >= 4 %:%"確認"%Q"%*script(%*RESULT markcount)エントリがマークされています"
            *execinarc %:%ME_editor
;;@E        , %"JMTE|Text edit"%Orib,editor %{%FDC%}
;@^E        エクスプローラで表示
F         , %k"APPS eo':'edw','r':' &DOWN &DOWN &DOWN &DOWN &DOWN &DOWN &DOWN &DOWN &DOWN %:%K"@F
;;@F        マスク
;@\F       保持マスク
;;\F        , %em *maskentry %"エントリマスク"%{.%T%}
^F        , *launch %0..\Everything\Everything.exe -p %FD
;;@^F      エクスプローラの検索
^\F       , *launch %0..\Everything\Everything.exe -s "%X"
;@G        窓入れ替え
;@^G       パス変更
H         , *if %*script(%*RESULT panecount) <= 1 %:%K"@LEFT %:*stop
            %K"@BS
;;@H       コマンドライン
\H        , %K"@h
^\H       , *linemessage execute > %hh0
            *execute %Hh0
I         , %Oai %K"@^\I
            *topmostwindow %*findwindowclass(PPeditW),1
            *focus C
;;@I(=\F2) ドライブ情報
^I        = @\END   ;選択反転
;;@^I      エントリ情報
;@^\I      連動エントリ情報
J         = @DOWN
;;@J       インクリメンタルサーチ(ダイアログ)
;@\J       インクリメンタルサーチ
^J        = @J
K         = @UP
;;@K       ディレクトリ作成
;@\K       エントリ作成メニュー
^K        = @K
L         , *if %*script(%*RESULT panecount) <= 1 %:%K"@RIGHT %:*stop
            %K"@C_DIR
;;@L       パス変更
;@\L      ドライブ移動メニュー
^L        , *makeListFile %"リストファイル作成"%{%|list%|.xlf%},1
M         , *MENUTYPE %:%M_FileMOVE,M
;;@M       移動
;@\M       エクスプローラ互換移動
;N         , *ppv %FDCB     ;用途不明
;@N(=\V)  PPV
^N        , *makefile %"新規ファイル"%{%|%*nowdatetime("yND-HM")%|.txt%}
;@O        ファイル比較
\O        , *execinarc %:%M_bin,W
;;@\O      コメント・ハッシュメニュー
P         , %"書庫の作成" %M_xpack,P
;;P           , *pack "%*script(%*RESULT makepath)|%~X|.zip" %Or-
;;@P       ,*pack "%2%\|%X|" %Or-
Q         , *JSCRIPT Qlose.js
;;@Q       終了(確認なし)
R         , *if %*script(%*RESULT dirtype) >= 63 %:reces /mn%C:%*input("%*regexp("%FC","s/(.*\\)(?!$)(.*)(\..*)/$1|$2|$3/")" -title:"書庫内ファイル名の変更" -mode:Rc -select:i) %FD %&%K"@^F5" %:*stop
            %K"@R"
;;@R(F2)   リネーム
\R        , %M_FileRename,F
;;@\R       詳細リネーム
;S        ソート
;\S       保持ソート
T         = @'='
;;@T(=F4)  ツリー表示
\T        , %j"%2"
;;@\T      保持ツリー表示
U         , %M_xunpack,U
;;@U       解凍
;@\U       解凍メニュー
V         , %ME_viewer
;;@V       ビューア
;@\V(=N)  PPV
;@^\V      クリップボードをショートカットとして貼り付け
W         , %0..\ckw\ckw.exe
;;W         , %k"TAB" %:*where ,,".txt,.cfg,.js,.ahk,.ini,.lua"
;;@W       順番書き換え/リストファイル書き込み ;※順番はFATのみ、リストファイルは内容書き換えに注意
;;@\W      コメントファイル作成
;@^W       ファイル検索
;@X        パラメータを指定して実行
Y         ,*JSCRIPT syncview.js
;;@Y       PPV(フォーカス移動なし)
\Y        =
;;\Y      連動ビュー
^Z        ,*JSCRIPT cmdUndo.js
}
;==========================================================================
;**インクリメンタルサーチ
;==========================================================================
-|KC_incs   =
KC_incs = {
SPACE     ,%k"ESC"
;SPACE    ,%K"@SPACE" ;マーク順
;\SPACE   ,%K"@\SPACE"    ;マーク逆
^SPACE    ,*JSCRIPT toggleHighlight.js
^M        ,*JSCRIPT toggleMark.js
^~M       ,*JSCRIPT toggleMark.js
^N        = @DOWN
^~N       = @DOWN
^P        = @UP
^~P       = @UP
;&F 前方一致／部分一致
;&O 通常一致／ローマ字一致
;&T 検索対象:ファイル/コメント/両方
}
;==========================================================================
;**一行編集・PPe兼用
;==========================================================================
;-|K_edit    =
K_edit  = { ; 一行編集/PPe共用,K_lied,K_ppeが優先
^V_HBB      , %k"HOME \END" ;ctrl + ";"
;@F6        = @TAB
;@&' '      ,%M_editc
APPS        , %M_editc
^APPS       , %M_editci
}
;==========================================================================
;**PPe
;==========================================================================
-|K_ppe =
K_ppe = {
^V_HBA  , %k"APPS s" %: *insert %eh,d %"Complete" %E ;ctrl + ":"
ESC     , %k"&F4"
F6      , *replacefile %0script\xTest.js
\F6     , *execute C,*JSCRIPT xTest.js
F1      , *help %"PPxHELP Topic"%E
F9      , *ppcust
^F9     , *ifmatch Px*,%*extract(C,%*name(C,%W)) %:*setcust @%W %:*linemessage Done. %:*stop
          *linemessage 設定ファイルではありません
\^H     , *execute C,%*selecttext
^J      , %k"\F2 J" ;ジャンプ
^L      , %k"^KD"   ;行コピー
^R      , %K"@F7    ;置換
}
;==========================================================================
;**一行編集
;==========================================================================
-|K_lied =
K_lied  = { ; 一行編集(K_liedに該当しない場合はK_edit参照)
FIRSTEVENT  , *ifmatch !パス移動,%W %:*stop %*getcust(_User:ep) == 0 %:*setcust _User:ep= 0 %:*setcust K_lied:-|LEFT= %:*setcust K_lied:-|RIGHT= %:*setcust K_lied:-|'@'= %:*setcust K_lied:-|F12= %:*setcust K_lied:-|\ENTER= %:*setcust K_lied:-|^s=
;メモに追記するようなコマンドがあれば便利かも
}
K_tree  = { ; 一般ツリー(キー制限有,V_xx 形式を推奨)
}
KB_edit = { ; PPb一行編集
}
;==========================================================================
;** PPVキーバインド
;==========================================================================
-|KV_main =
KV_main   = {
-|FIRSTEVENT  ,
CLOSEEVENT  , *setcust X_bg:O_%n=100
              *zoom -2
              *linecust NoMask,KV_main:CLOSEEVENT,
;@'^'    メニューバー表示切替
;@'@'    文字コード切り替え
;@':'    表示形式切り替え
;@';'    桁数変更
;@'+'(=INS)  拡大
;@'-'(=del)  縮小
;@'='    拡縮モード切替
;@'<','>'  部分表示位置変更
;@DOWN   ,%KC"@DOWN@N
^DOWN       , *PPVUD DOWN
;;@^DOWN PPCのカーソルと連動
;@&DOWN    窓枠移動
;@\&DOWN   〃サイズ調整
;@UP   ,%KC"@UP@N
^UP         , *PPVUD UP
;;@^UP PPCのカーソルと連動
;@&UP    窓枠移動
;@\&UP   〃サイズ調整
;@LEFT   ,%KC"@LEFT@N"
;@^LEFT    PPCのカーソルと連動
;@&LEFT    窓枠移動
;@\&LEFT   〃サイズ調整
;@RIGHT    ,%KC"@RIGHT@N"
;@^RIGHT   PPCのカーソルと連動
;@&RIGHT   窓枠移動
;@\&RIGHT  〃サイズ調整
;@END    ,%KC"@'>'@N"
;@HOME   ,%KC"@'<'@N"
;@&HOME    記憶した窓位置を復元
;@&\HOME   窓位置を記憶
;@ENTER(=N,BS,\ESC)  最小化
;@ESC    終了(確認あり)
TAB         , *JSCRIPT keyTAB_PPv.js
F1          , *cd %0 %:*help %"PPxHELP Topic"%E
;@F1   Help
;@F2   = @\F3 ?
;@F3   検索昇降
;@&F4(=Q)  終了
;@F5(=.)   再読み込み
F6          , *execute C,editorC %0script\xTest.js
\F6         , *JSCRIPT xTest.js
;@&F6    窓枠サイズ最適化
F9          = CUSTOMIZE
;@F10    メニュー表示
;@\F10(^ENTER) ファイル判別メニュー
;@^\F10    シェルメニュー
0           , %v%M_menuVRecent,1
\V_H30      , *addhistory x,%*regexp("%W","s/(PPV\[[A-Z]\])(.*)/$2/")   ;SHIFT + 0
              *linemessage Bookmark >%hx0
;@B,^B(=F,^F)  検索
;@C    制御記号トグル
;@D    栞1を挟む
;@^D   栞1~4を挟む
;@E(\ENTER)  エディタで開く
;@\E,^E    ppeで開く
;@F,^F(=B,^B)  検索
;@G    栞1・直前の位置にジャンプ
;@^G   栞1~4にジャンプ
H           = NULL
;;@H   コマンド実行
\H          , %K"@H
;@J    行ジャンプ
;@M    コメント表示  httpで取得したファイルのヘッダ
N           , *selectppx C
;@^O   ファイルを開く
;@P    再生
;@^P   印刷
Q           , *ifmatch "/V[XYZ]/",%n %:*execute C,*JSCRIPT syncview.js %:*stop
              %K"@Q
;;@Q(=&F4) 終了
;@^S   保存
;@T    表示/論理行トグル
;@U    行番号トグル
;@^U   画像印刷時の解像度入力
;@^\V    形式を指定して貼り付け
;@W    フォントトグル
;@X    現ファイルをパラメータ指定実行
Y           , *ifmatch "/V[XYZ]/",%n %:*execute C,*JSCRIPT syncview.js %:*stop
;Z    開く
}
;==========================================================================
;** テキスト(ページ)用設定
;==========================================================================
-|KV_page =
KV_page   = {
LOADEVENT , *setcust MV_click:L_SPC=@^ENTER
            *customize XV_drag=0,1,3,2
            *linemessage %*extract(C"%%*comment")
;@^DOWN    ,%KC"@DOWN@N
;@^UP    ,%KC"@UP@N
E         , *topmostwindow %N,0
            editorC %FDC -g%L -c%lH
;I       = NULL
;@I(=5)    ページ/キャレットモード切替
}
;==========================================================================
;** テキスト(キャレット)用設定
;==========================================================================
-|KV_crt  =
KV_crt    = {
;@'\'    =@PDOWN
;@'/'    =@PUP
;;@'/'部分表示切替
E         , *topmostwindow %N,0
            editorC %FDC -g%L -c%lH
}
;==========================================================================
;** 画像用設定
;==========================================================================
-|KV_img  =
KV_img    = {
LOADEVENT , *ifmatch "/https?/",%W
            *addhistory x,%*regexp("%W","s/(PPV\[[A-Z]\])(.*)/$2/")
            *setcust MV_click:L_SPC=
            *customize XV_drag=1,0,3,2
            *linemessage %*extract(C"%%*comment")
DOWN      , *PPVUD DOWN
LEFT      , *PPVUD LEFT
RIGHT     , *PPVUD RIGHT
UP        , *PPVUD UP
[         , *execute C,*comment %eRg%"コメントの編集""%{%*extract(C"%%*comment")%}
D         , *JSCRIPT cmdDeletePPv.js
O         , *JSCRIPT Opacity.js
^S        , %k"\d':'/workspace/%*regexp("%R","s/.*\/(.*\.*)/$1/")" %:%K"@^s
T         , *topmostwindow %N
            *linemessage 最前面トグル
V         , viewer %FDC
X         = @'-'
Z         = @'+'
SPACE     , *PPVUD SPACE
}