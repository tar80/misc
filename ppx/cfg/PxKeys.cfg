PPxCFG  = 
;---------------------------------------------------------------------------
;・キーバインド
; ※;@はデフォルトコマンド
;/ K_tray/ KC_main/ KC_incs/ K_edit/ K_ppe/ K_lied/ K_tree/
;/ KB_edit/ KV_main/ KV_page/ KV_crt/ KV_img/ K_git/
;---------------------------------------------------------------------------
;** PPtray
;---------------------------------------------------------------------------
; -|K_tray =
; K_tray = {
; &':'         , *setcust X_ltab=1,6
;                %*input(-title:"Launch.." -mode:e -k *editmode h)
; }
;---------------------------------------------------------------------------
;** PPc
;---------------------------------------------------------------------------
-|KC_main =
KC_main = {
FIRSTEVENT  , *string i,cID=%*rightstr(%n,1)
              *set path+=C:\bin\git\cmd;C:\bin\git\mingw64\libexec\git-core;C:\bin\git\usr\bin;C:\bin\node\v13110 ;portable_git用にpath追加
              *set HOME=C:\bin\home
;SELECTEVENT , *comment 1,extract,%*script(%*RESULT(filetype))
\V_H30      , *addhistory u,%FD             ;shift + "0"
              *linemessage Bookmark > %FD
^V_H30     , %M_pjump1,W               ;ctrl + "0"
1           , %j"%*getcust(_User:pk1)"
2           , %j"%*getcust(_User:pk2)"
3           , %j"%*getcust(_User:pk3)"
4           , %j"%*getcust(_User:pk4)"
5           , %j"%*getcust(_User:pk5)"
6           , %j"%*getcust(_User:pk6)"
;7          , %j"%*getcust(_User:pk7)"
;8          , %j"%*getcust(_User:pk8)"
9           , %j%*name(HP,%1)%'trash'%\deleted$
'!'         , %K~,"1"
'"'         , %K~,"2"
'#'         , %K~,"3"
'$'         , %K~,"4"
'%'         , %K~,"5"
'&'         , %K~,"6"
;'''        , %K~,"7"
;'('        , %K~,"8"
;@'-'        マーク解除(ダイアログ)
;@&'-'       タブメニュー
;@'+'        マーク(ダイアログ)
;@';'        表示形式変更(MC_celS)
;@'>'        (=\PgDn,^PgDn)  末尾に移動
;@'<'        (=\PgUp,^PgUp)  先頭に移動
;@'^'        メニューバーON/OFF
'['         , *comment edit
'{'         , *comment clear
              *linemessage !"コメント削除
; ^V_HBC      = @PUP      ; ^,ページ上移動
; ^V_HBE      = @PDOWN    ; ^.ページ下移動
;;@'*'       すべて選択(※除ディレクトリ)
'@'         , *string i,ltab=%*getcust(X_ltab)
              *setcust X_ltab=1,4
              *string o,path="%*regexp("%FDVN","s/(.*\\)(?!$).*/$1/")"
              *jumppath "%*input(%so"path" -title:"Jumppath.." -mode:e -k *editmode d)"
\V_HC0      , *string i,ltab=%*getcust(X_ltab)
              *setcust X_ltab=1,4
              *string o,path="%*regexp("%FDVN","s/(.*\\)(?!$).*/$1/")"
              *execute ~,*jumppath "%*input(%so"path" -title:"Jumppath..OP" -mode:e -k *editmode d)"
':'         , *string i,ltab=%*getcust(X_ltab)
              *setcust X_ltab=1,4
              *execute %n,%*input(-title:"Execute.." -mode:h)
^V_HBA      , *linemessage !"execute > %hh0
              *execute %n,%Hh0
'\'         = @BS
'.'         , *linemessage !"filetype: %*RESULT(filetype)
              *entrytip fileinfo
'?'         , *range firstmark,cursor -mark
APPS        , *script %'scr'%\keyApps.js,M_Ccr
^APPS       , %M_DirMenu,W
;;@         , ディレクトリメニュー
^&APPS      , %M_menuCTool
;@DEL       エントリ位置記憶
\Down       , %K~"@Down"
;;@\Down(=PgDn)  ページ移動
^Down       , *cursor 16,1,3,B0010,5,B0011  ;次のマークに移動
\^DOWN      , *script %'scr'%\moveDir.js,1,%'temp'%\ppxcomp.tmp
END         , *pane select t+1
;@Enter     実行
\ENTER      , %ME_scr   ;デフォルトの動作ではディレクトリの判定が異なる
;;@\Enter    ファイル判別
&ENTER      , %K"@=" %K~"C_DIR
;@^ENTER(=\F10)  ファイル判別メニュー
~ENTER      , %j"%FDC::listfile"
;@&\ENTER   連動プロパティ
;@ESC       終了(確認あり)
HOME        , *pane select t-1
;@INS       エントリ位置移動
;@^LEFT     前に戻る
;@^\LEFT    まとめて前に戻る
;@Pause     処理中止
;@^Pause    応答無し状態の解消(※Vista以降)
;@^RIGHT    次に進む
;@^\RIGHT    まとめて次に進む
^SPACE      , *js "PPx.EntryHighlight = (PPx.EntryHighlight == 0 ? 2 : 0)"
\^SPACE     , *script %'scr'%\toggleMarkHighlight.js,2
TAB         , *script %'scr'%\keyTAB.js
\TAB        , *focus ~
;;@\TAB       ,*Pane NewTab
^TAB        , *focus "PPB["
\Up         , %K~"@Up"
;;@\UP(=PgUp)  ページ移動 
^UP         , *cursor 16,-1,3,B0010,5,B0011 ;前のマークに移動
\^UP        , *script %'scr'%\moveDir.js,0,%'temp'%\ppxcomp.tmp
F1          , *help
;@F2      リネーム
;@\F2(=I) ドライブ情報
F3        , *if %*RESULT(Syncview) != 0 %: %K~"]"
\F3       , *if %*RESULT(Syncview) != 0 %: %K~"["
;;@F3(=^F) エクスプローラの検索
F4        , *string o,ppe=%*findwindowclass(PPeditW)
            *if %so"ppe" != 0 %: *focus #%so"ppe" %: *stop
            *ppe %R
;;@F4(=T)  ツリー表示
\F4       , *string o,ppe=%*findwindowclass(PPeditW)
            *if %so"ppe" != 0 %: *focus #%so"ppe" %: *stop
            *ppe -new -k *setcaption PPe
;@&F4       全終了
;@F5       更新
;;@^F5      表示ディレクトリの更新
F6        , gvim --remote-tab-silent %'repoppx'%\xTest.js
^F6       , *script %'repoppx'%\xTest.js
            *linemessage Run >>> xTest.js
^\F6      , *linemessage Result >>> %*script(%'repoppx'%\xTest.js)
F8        , %M?layoutmenu,!P    ;スクロールバー縦横切り替え
F9        = CUSTOMIZE
^F9       , %M_menuCView,!A
F10       , *RotateExecute u_pos,"*windowposition %%N.,700,400","*windowposition %%N.,350,120","*windowposition %%N.,700,0"
;;@F10     メニューバーにフォーカス移動
;@\F10(=^ENTER)  ファイル判別メニュー
;@^\F10     Shell Context Menu
;@F11       新規タブ
\F11      , *script %'scr'%\callNewPPx.js
;;@\F11     同パス新規タブ
F12       , *script %'scr'%\rotateViewstyle.js
;;@F12      ファイル複製
;@\F12      ハードリンク作成
;@A         属性変更
B         , *if %*RESULT(EntryMark) == 0 %: *markentry *.%t %: *stop
            *unmarkentry *.%t
;;@B       , %"Binary Convert" %M_bin
C         , *script %'scr'%\keyApps.js,M_FileCOPY
;;@C        コピー
\C        , %M_menuCEdit1
;@^C        エクスプローラ互換コピー
;@^\C       ディレクトリ名をクリップ
D         , *if %*RESULT(DirectoryType) >= 62 %: %"ファイル操作" %Q"書庫から削除します" %: %u7-zip64.dll,d -hide %FD %@ %: *stop
            *if %*RESULT(DirectoryType) == 9 %: *delete "%C" %: *stop
            *if %*RESULT(DirectoryType) == 4 %: %K"@^\D" %: *stop
            %"ファイル操作" %Q"選択エントリを削除します" %: *ppcfile !safedelete,%*name(HP,%1)%'trash'%\, -qstart -min -retry:0 -error:0
;;@D        ゴミ箱へ
; \D        , *string o,Path=%#;FDCN
;             %"Delete?"%Q"%*regexp(%so"Path",tr/;/\n/) %: %On %0..\FastCopy\FastCopy.exe /open_window /no_confirm_del /cmd=delete /wipe_del /auto_close "%*regexp(%so"Path","s/;/"" ""/g")"
;;@\D       削除
;@^\D       リストファイルから削除
;@^D        自動D&D
E         , *if %*RESULT(EntryMarkCount) >= 4 %: %"確認"%Q"%*RESULT(EntryMarkCount)エントリがマークされています%bn続行しますか？"
            %ME_editor
;;@E        , %"JMTE|Text edit"%Orib,editor %{%FDC%}
\E        , %OP gvim -p +1 +/"%hs0" %#FDC
;@^E        エクスプローラで表示
F         , *ifmatch !CX,%n %: *linecust win_pos,K_edit:FIRSTEVENT,*windowposition %%*findwindowtitle("エントリマスク"),%%*windowrect(%%NC#,l),%%*windowrect(%%NC,b) %%: *js "PPx.Execute('*linecust win_pos,K_edit:FIRSTEVENT,')"
            %k"o':'ed','r':'
            %Os %K"@F" %: *deletehistory m,0
;;@F        マスク
;@\F       保持マスク
;;\F        , %em *maskentry %"エントリマスク"%{.%T%}
^F        , *launch %0..\Everything\Everything.exe -p %FD
;;@^F      エクスプローラの検索
^\F       , *launch %0..\Everything\Everything.exe -s "%X"
G         , *if %*RESULT(repository) != -1 %: *script %'scr'%\gitMode.js %: *stop
            *ppb
^G        , *if %*RESULT(repository) != -1 %: %M_Cgit,A
;;@G        窓入れ替え
;@^G       パス変更
H         = @LEFT
;;@H       コマンドライン
\H        , *if %*RESULT(Pane.Count) < 2 %: %K"@PUP" %: *stop
I         , %Oi %K"@^\I"
            *topmostwindow %*findwindowclass(PPeditW),1
            *focus %n
;;@I(=\F2) ドライブ情報
^I        = @\END   ;選択反転
;;@^I      エントリ情報
;@^\I      連動エントリ情報
J         , *cursor 1,1,4,B0000,5,B0011
;;@J       インクリメンタルサーチ(ダイアログ)
;@\J       インクリメンタルサーチ
^J        = @J
K         , *cursor 1,-1,4,B0000,5,B0011
;;@K       ディレクトリ作成
;@\K       エントリ作成メニュー
^K        = @K
L         = @RIGHT
\L        , *if %*RESULT(Pane.Count) < 2 %: %K"PDOWN" %: *stop
            %K"@\L"
;;@L       パス変更
;@\L      ドライブ移動メニュー
^L        , *makeListFile %"リストファイル作成"%{%|list%|.xlf%} -marked
M         , *script %'scr'%\keyApps.js,M_FileMOVE
;;@M       移動
;@\M       エクスプローラ互換移動
N         , *setcust X_vpos=0
            *if 4==%*RESULT(DirectoryType) %: *script %'scr'%\LFexec.js,ppv %: *stop
            *ppv -bootid:c %FDC
\N        , *setcust X_vpos=0 %: *script %'scr'%\callNewPPx.js,V
;@N(=\V)  PPV
^N        , *makefile %"ファイル作成"%{%|%*nowdatetime("yND-HM")%|.txt%}
;@O        ファイル比較
\O        , *execinarc %: %M_bin,D
;;@\O      コメント・ハッシュメニュー
P         , %"書庫の作成" %M_xpack,P
;;@P       ,*pack "%2%\|%X|" %Or-
Q         , *script %'scr'%\Qlose.js
;;@Q       終了(確認なし)
^Q        , *dock delete,t,input K_git
            *closeppx BA
R         , *checkoffmark
            *if %*RESULT(EntryState) == 1 %: *stop
            *if %*RESULT(DirectoryType) == 4 %: %K"@R %: *stop
            *ifmatch "63;64;96",%*RESULT(DirectoryType) %: %On *ppb -c reces /mn%C:%*input("%*regexp("%FC","s/(.*\\)(.*)/$1|$2|/")" -title:"書庫内ファイル名の変更" -mode:Ec -select:i) %FD %&%K"@^F5" %: *stop
            *ppcfile !rename -min -same:skip -error:dialog -log:off -name:"%*input("%C" -title:"Rename" -mode:Ec -select:fs)"
;;@R(F2)   リネーム
\R        , *ppcfile rename -same:skip -log:off
;;@\R       詳細リネーム
^R        , %M_FileRename,S
;S        ソート
;\S       保持ソート
^S        , *if %*RESULT(DirectoryType)==4 %: %"リストの保存"%Q"コメント、並び、マーク状態を保存します" %: *script %'scr'%\LFsave.js
;;^S        = @W
T         = @'='
;;@T(=F4)  ツリー表示
\T        , %j"%2"
;;@\T      保持ツリー表示
U         , %M_xunpack,U
;;@U       解凍
;@\U       解凍メニュー
V         , %ME_viewer
;;@V       ビューア
;@\V(=N)  PPV
;@^\V      クリップボードをショートカットとして貼り付け
W         , *if %*RESULT(EntryMarkCount) == 0 %: *where -listfile:"%'temp'%\ppxresult.xlf" %: *stop
            *where -path:"%#;C" -listfile:"%'temp'%\list\ppxresult.xlf"
;;@W       順番書き換え/リストファイル書き込み ;※順番はFATのみ、リストファイルは内容書き換えに注意
\W        , %k"&t ESC" %: *if %*RESULT(EntryMarkCount) == 0 %: *where -listfile:"%'temp'%\ppxresult.xlf" %: *stop
            *where -marked -listfile:"%'temp'%\ppxresult.xlf"
;;@\W      コメントファイル作成
^W        , *if %*RESULT(DirectoryType)==4 %: *script %'scr'%\commentSearch.js,1 %: *stop
            *string o,xlf="%'tmp'%\ppxresult.xlf"
            %Oi *script %'scr'%\LFgrep.js,%so"xlf"
            *ppc -r -single -mps -bootid:W %so"xlf" -k *viewstyle -temp &GREP
;;@^W       ファイル検索
\X        , *setcust XC_celD=_DBLA,_AUTO,6,5
            *ppc -r -max -single -mps -bootid:X "%1" -k *execute CX,*maskentry .bmp,.jpg,.jpeg,.png,.gif,.vch,.edg,.mpg,p:*
;@X        パラメータを指定して実行
Y         , *script %'scr'%\syncview.js
;;@Y       PPV(フォーカス移動なし)
\Y        = NULL
;;\Y      連動ビュー
^Z        , *script %'scr'%\cmdUndo.js
}
;---------------------------------------------------------------------------
;** インクリメンタルサーチ
;---------------------------------------------------------------------------
-|KC_incs   =
KC_incs = {
SPACE     = @ESC
;SPACE    , %K"@SPACE" ;マーク順
;\SPACE   , %K"@\SPACE"    ;マーク逆
^SPACE    , *js "PPx.EntryHighlight = (PPx.EntryHighlight == 1 ? 2 : 1)"
^M        , *script %'scr'%\toggleMarkHighlight.js,1
^~M       , *script %'scr'%\toggleMarkHighlight.js,1
^N        = @DOWN
^~N       = @DOWN
^P        = @UP
^~P       = @UP
;&F 前方一致／部分一致
;&O 通常一致／ローマ字一致
;&T 検索対象:ファイル/コメント/両方
}
;---------------------------------------------------------------------------
;** 一行編集・PPe兼用
;---------------------------------------------------------------------------
-|K_edit =
K_edit  = { ; 一行編集/PPe共用,K_lied,K_ppeが優先
^V_HBA  , *insert "%*script(%'scr'%\compCode.js,"ih","""","Complete..")"  ;ctrl + ":"
^V_HBB  , %k"HOME \END" ;ctrl + ";"
;@F6    = @TAB
;@&' '  ,%M_editc
APPS    , *setcust M_editc:mode=??M_editcPath
          *ifmatch "/ファイル操作/",%W %: *setcust M_editc:mode=??M_editcRename
          %M_editc
^APPS   , %M_editci
; ^G    , *ifmatch ファイル操作*,%W %: %M_editcRename,F
^D      , *script %'scr'%\cmdDeleteHistory.js,h
^N      = @DOWN
^P      = @UP
}
;---------------------------------------------------------------------------
;** PPe
;---------------------------------------------------------------------------
-|K_ppe =
K_ppe = {
ESC     , %k"&F4
F6      , *replacefile %'scr'%\xTest.js
\F6     , *execute C,*script %'scr'%\xTest.js
F1      , *help
F4      , *focus C
F9      , *ppcust
^F9     , %M_menuEView,!A
\^V_HBA , *execute C,%*selecttext() ; shift + ctrl + ":"
^G      , %k"\F2 J ;ジャンプ
^P      , %k"^KD   ;行コピー
^R      , %K"@F7"  ;置換
}
;---------------------------------------------------------------------------
;** 一行編集
;---------------------------------------------------------------------------
-|K_lied =
K_lied  = { ; 一行編集(K_liedに該当しない場合はK_edit参照)
FIRSTEVENT  , *ifmatch !0,0%si"ltab" %: *setcust X_ltab=%si"ltab"
F12     , *ifmatch Jumppath..,%W %: *launch %0fenrirScan.exe %& *completelist /reload %: *stop
          %K"@F12"
^H      , *string o,complist=%'tmp'%\ppxcomp.tmp
          *replace %*script(%'scr'%\compPath.js,%so"complist")
          %K"@F2"
;;@^H     (=BACKSPACE)
^L      , *if 0%N-L > 0 %: %K"@F4"
          *ifmatch "o:e,a:d+",%*regexp(%*edittext,"s/.*\s(.*)/$1/") %: *replace %*edittext()%\
          %K"@F2@TAB"
          *script %'scr'%\selStr.js,"(.*\\(?!$)(.*))"
; ^N      = @DOWN
;;@^N     カーソル位置ファイル名(=^R)
; ^P      = @UP
;;@^P     カーソル位置フルパス(=&P)
^S      , *ifmatch Jumppath*,%W %: *linemessage [Add] %*edittext() %: %On *ppb -c echo +%*edittext()>> %0ScanRule.ini %: *stop
          *ifmatch Execute*,%W %: *addhistory h,"%*script(%'scr'%\compcode.js,"e","""")" %: *linemessage    Add history %: *stop
          %K"@^S
}
K_tree  = { ; 一般ツリー(キー制限有,V_xx 形式を推奨)
}
;---------------------------------------------------------------------------
;** PPVキーバインド
;---------------------------------------------------------------------------
-|KV_main =
KV_main   = {
; FIRSTEVENT  , *ifmatch "/https?/",%W %: *addhistory x,%*regexp("%W","s/(PPV\[[A-Z]\])(.*)/$2/")
LOADEVENT   , *customize XV_drag=0,1,3,2
              *setcust MV_click:L_SPC=@^ENTER
              *setcust @%'cfg'%\zz2HMppvDoc.cfg
              *script %'scr'%\movingPPv.js
              *linecust Do,KV_main:ACTIVEEVENT,%%K"@LOADCUST"
ACTIVEEVENT , *setcust MV_click:L_SPC=@^ENTER
              *setcust XV_drag=0,1,3,2
              *setcust @%'cfg'%\zz2HMppvDoc.cfg
CLOSEEVENT  , *setcust X_bg:O_%n=100
              *zoom -2
              *linecust mask,KV_main:CLOSEEVENT,
;@'^'    メニューバー表示切替
;@'@'    文字コード切り替え
;@':'    表示形式切り替え
;@';'    桁数変更
;@'+'(=INS)  拡大
;@'-'(=del)  縮小
;@'='    拡縮モード切替
;@'<','>'  部分表示位置変更
;@DOWN   ,%KC"@DOWN@N
^DOWN       , *PPVUD DOWN
;;@^DOWN PPCのカーソルと連動
;@&DOWN    窓枠移動
;@\&DOWN   〃サイズ調整
;@UP   ,%KC"@UP@N
^UP         , *PPVUD UP
;;@^UP PPCのカーソルと連動
;@&UP    窓枠移動
;@\&UP   〃サイズ調整
;@LEFT   ,%KC"@LEFT@N"
;@^LEFT    PPCのカーソルと連動
;@&LEFT    窓枠移動
;@\&LEFT   〃サイズ調整
;@RIGHT    ,%KC"@RIGHT@N"
;@^RIGHT   PPCのカーソルと連動
;@&RIGHT   窓枠移動
;@\&RIGHT  〃サイズ調整
;@END    ,%KC"@'>'@N"
;@HOME   ,%KC"@'<'@N"
;@&HOME    記憶した窓位置を復元
;@&\HOME   窓位置を記憶
;@ENTER(=N,BS,\ESC)  最小化
;@ESC    終了(確認あり)
TAB         , *script %'scr'%\keyTAB.js
F1          , *help
;@F1   Help
;@F2   = @\F3 ?
;@F3   検索昇
;@\F3   検索降
;@&F4(=Q)  終了
;@F5(=.)   再読み込み
F6          , *execute C,gvim --remote-tab-silent %0script\xTest.js
^F6         , *script %'scr'%\xTest.js
;@&F6    窓枠サイズ最適化
F9          = CUSTOMIZE
;@F10    メニュー表示
;@\F10(^ENTER) ファイル判別メニュー
;@^\F10    シェルメニュー
0           , %v%M_menuVRecent,A
\V_H30      , *addhistory x,%*regexp("%W","s/(PPV\[[A-Z]\])(.*)/$2/")   ;shift + 0
              *linemessage Bookmark >%hx0
':'         , *linecust Do,KV_img:ACTIVEEVENT,
              *linecust Do,KV_main:ACTIVEEVENT,
              *execute %n,%*input(-title:"Execute.." -mode:e -k *editmode h)
;@B,^B(=F,^F)  検索
;@C    制御記号トグル
;@D    栞1を挟む
;@^D   栞1~4を挟む
;@E(\ENTER)  エディタで開く
;@\E,^E    ppeで開く
F           = @F3
;@F,^F(=B,^B)  検索
\F          = @\F3
;@\F,   ハイライト単語
;@G    栞1・直前の位置にジャンプ
;@^G   栞1~4にジャンプ
H           = NULL
;;@H   コマンド実行
; \H          , %K"@H
;@J    行ジャンプ
;@M    コメント表示  httpで取得したファイルのヘッダ
N           , *script %'scr'%\callNewPPx.js
;@^O   ファイルを開く
;@P    再生
;@^P   印刷
Q           , *script %'scr'%\Qlose.js
;;@Q(=&F4) 終了
;@^S   保存
;@T    表示/論理行トグル
;@U    行番号トグル
;@^U   画像印刷時の解像度入力
;@^\V    形式を指定して貼り付け
;@W    フォントトグル
;@X    現ファイルをパラメータ指定実行
Y           , *execute C,*script %'scr'%\syncview.js
;Z    開く
}
;---------------------------------------------------------------------------
;** テキスト(ページ)用設定
;---------------------------------------------------------------------------
-|KV_page =
KV_page   = {
; @^DOWN    ,%KC"@DOWN@N
; @^UP      ,%KC"@UP@N
E         , *topmostwindow %N,0
            gvim --remote-tab-silent +%L %FDC
; I         = NULL
;@I(=5)    ページ/キャレットモード切替
}
;---------------------------------------------------------------------------
;** テキスト(キャレット)用設定
;---------------------------------------------------------------------------
-|KV_crt  =
KV_crt    = {
;@'\'    =@PDOWN
;@'/'    =@PUP
;;@'/'部分表示切替
E         , *topmostwindow %N,0
            gvim --remote-tab-silent +%L %FDC
}
;---------------------------------------------------------------------------
;** 画像用設定
;---------------------------------------------------------------------------
-|KV_img  =
KV_img    = {
LOADEVENT   , *setcust MV_click:L_SPC=
              *setcust XV_tmod=0
              *setcust _User:u_rotate_PPvBG=1
              *setcust @%'cfg'%\zz2HMppvImg.cfg
              *customize XV_drag=1,0,3,2
              *script %'scr'%\movingPPv.js
              *linecust Do,KV_img:ACTIVEEVENT,%%K"@LOADCUST"
ACTIVEEVENT , *setcust MV_click:L_SPC=
              *setcust XV_drag=1,0,3,2
              *setcust @%'cfg'%\zz2HMppvImg.cfg
DOWN      , *PPVUD DOWN
LEFT      , *PPVUD LEFT
RIGHT     , *PPVUD RIGHT
UP        , *PPVUD UP
[         , *execute C,*comment %eRg%"コメントの編集""%{%*extract(C"%%*comment")%}
D         , *script %'scr'%\cmdDeletePPv.js
O         , *script %'scr'%\Opacity.js
J         = NULL
^S        , %k"\c':'/bin/temp/%FV" %: %K"@^s
T         , *topmostwindow %N
            *linemessage 最前面トグル
V         , viewer %FDC
X         = @'-'
Z         = @'+'
SPACE     , *PPVUD SPACE
}
;---------------------------------------------------------------------------
;** PPBキーバインド
;---------------------------------------------------------------------------
-|KB_edit =
KB_edit = {
\INS    , %K"@^v"
^INS    , %K"@^x"
^TAB    , *focus C
^D      = @ESC
; ^G      , *cd %'repo' %: git ls-files | peco
^Q      , *execute C,*dock delete,t,K_git
          %K"@ESC"
; ^T      , git log --oneline | peco | cut -d' ' -f1
}
;---------------------------------------------------------------------------
;** dock input git
;---------------------------------------------------------------------------
-|K_git =
K_git  = {
ENTER   , *execute %n,git %*edittext() %: %K"@^y
          %mgitcheckout
          *linecust gitcheckout,K_git:ENTER,
^V_HBA  , *insert %*input(-title:"コミットの選択" -mode:e -k *COMMITLIST)
^TAB    , *focus "PPB["
^SPACE  , *execute C%*regexp("%W","s/PPC\[(\w)\].*/$1/"),%%K"SPACE
^' '    = NULL
^UP     , *execute C%si"cID",%%K"UP
^DOWN   , *execute C%si"cID",%%K"DOWN
F5      , *execute C%si"cID",%%K"F5
^B      , *script %'scr'%\gitCheckout.js
^G      , %M_Cgit,C
^N      = @DOWN
^P      = @UP
^Q      , *pptray -c *execute C,*dock delete,t,K_git
          *closeppx BA
^V_HC0  , *postmessage %NBA,258,81,0 ; PPbにQのキーコードを送る
^S      , @echo %*edittext()| nkf -w -Lu >> %'repoppx'%\list\GITCOMMAND.TXT
          *logwindow "add GITCOMMAND.TXT > '%*edittext()'"
}
